apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: "com.github.dcendents.android-maven"
apply plugin: 'com.jfrog.bintray'

ext {
    siteUrl = 'https://github.com/joy-inc/core-http'// 项目的主页
    gitUrl = 'git@github.com:joy-inc/core-http.git'// Git仓库的url

    libraryDescription = 'core-http'// 项目描述
    libraryName = "core-http"// 发布到JCenter上的项目名字
}

version = "0.0.8"// This is the library version used when deploying the artifact
group = "com.joy.support"// Maven Group ID for the artifact，一般填你唯一的包名

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 8
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile rootProject.ext.dependencies.volley
    compile rootProject.ext.dependencies.fastjson
    compile rootProject.ext.dependencies.rxjava
    compile rootProject.ext.dependencies.rxandroid
//    compile 'com.squareup.okhttp3:okhttp:3.5.0'
//    compile 'com.squareup.retrofit2:retrofit:2.1.0'
//    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name libraryDescription
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'daishiwen'// 填写的一些基本信息
                        name 'daishiwen'
                        email 'daishiwen1212@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
//    archives javadocJar // 暂不生成javadoc文件
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "joy-library"
        name = libraryName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
