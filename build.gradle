apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

ext {
    publishedGroupId = 'com.qyer.library'
    libraryName = 'QYHttp'
    artifact = 'QYHttp'

    libraryDescription = 'Android Library to provide app new version check and update'
    libraryVersion = '0.0.4'

    siteUrl = 'http://git.2b6.me/Android/QYHttp'
    gitUrl = 'git@git.2b6.me:Android/QYHttp.git'

    developerId = 'daishiwen'
    developerName = 'daishiwen'
    developerEmail = 'daishiwen1212@gmail.com'
    developerOrganization = 'QYER Inc'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 24
        versionCode 4
        versionName '0.0.4'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
    }
}

dependencies {
    compile rootProject.ext.dependencies.volley
    compile rootProject.ext.dependencies.fastjson
    compile rootProject.ext.dependencies.rxjava
    compile rootProject.ext.dependencies.rxandroid
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId publishedGroupId
            version libraryVersion
            artifactId project.getName()
            artifact "$buildDir/outputs/aar/${project.getName()}-release.aar"
            artifact "$buildDir/intermediates/bundles/release/classes.jar"
            artifact sourceJar
//            artifact source: sourceJar, classifier: 'src', extension: 'zip'

            pom.withXml {
                def root = asNode()
                root.appendNode('description', libraryDescription)

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', licenseName)
                license.appendNode('url', licenseUrl)
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', developerId)
                developer.appendNode('name', developerName)
                developer.appendNode('email', developerEmail)
                developer.appendNode('organization', developerOrganization)

                def dependenciesNode = root.appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'http://172.1.1.227:9999/artifactory'
    publish {
        repository {
            repoKey = libraryVersion.endsWith('-SNAPSHOT') ? 'qyer-snapshot' : 'qyer-release'
            username = 'deployer'
            password = 'Dep10yeR'
        }

        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'dev.team': 'core']
            publishPom = true
        }
    }
}